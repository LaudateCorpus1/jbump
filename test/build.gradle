apply plugin: 'application'
java.targetCompatibility = JavaVersion.VERSION_1_8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

sourceCompatibility = 1.8 // lwjgl3 needs this

mainClassName = "com.dongbat.jbump.test.TestBump"
def projectName = 'jbump-test'

dependencies {
    implementation project(":jbump")
    implementation "com.badlogicgames.gdx:gdx:1.10.0"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:1.10.0"
    implementation "com.badlogicgames.gdx:gdx-platform:1.10.0:natives-desktop"
    implementation "space.earlygrey:shapedrawer:2.4.0"
}

def os = System.properties['os.name'].toLowerCase()

run {
    setIgnoreExitValue(true)

    if (os.contains('mac')) {
        // Required to run LWJGL3 Java apps on MacOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Main-Class': "com.dongbat.jbump.test.TestBump"
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    dependencies {
        exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
    }
    doLast {
        file(archiveFile).setExecutable(true, false)
    }
}
